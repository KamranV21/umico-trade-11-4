
// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не Параметры.Свойство("РежимВвода", РежимВвода) Тогда
		РежимВвода = 0;
	КонецЕсли;

	Параметры.Свойство("НомерСчета", НомерСчета);
	Параметры.Свойство("ВариантИспользования", Объект.ВариантИспользования);
	Параметры.Свойство("КассаККМ", Объект.КассаККМ);

	ОпределитьПараметрыПодключения();

	ПервоеНажатие = Истина;

	УстановитьВидимость();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Кнопка0(Команда)

	ДобавитьЧисло("0");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка1(Команда)

	ДобавитьЧисло("1");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка2(Команда)

	ДобавитьЧисло("2");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка3(Команда)

	ДобавитьЧисло("3");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка4(Команда)

	ДобавитьЧисло("4");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка5(Команда)

	ДобавитьЧисло("5");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка6(Команда)

	ДобавитьЧисло("6");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка7(Команда)

	ДобавитьЧисло("7");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка8(Команда)

	ДобавитьЧисло("8");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка9(Команда)

	ДобавитьЧисло("9");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка0ПраваяКлавиатура(Команда)

	ДобавитьЧисло("0");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка1ПраваяКлавиатура(Команда)

	ДобавитьЧисло("1");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка2ПраваяКлавиатура(Команда)

	ДобавитьЧисло("2");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка3ПраваяКлавиатура(Команда)

	ДобавитьЧисло("3");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка4ПраваяКлавиатура(Команда)

	ДобавитьЧисло("4");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка5ПраваяКлавиатура(Команда)

	ДобавитьЧисло("5");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка6ПраваяКлавиатура(Команда)

	ДобавитьЧисло("6");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка7ПраваяКлавиатура(Команда)

	ДобавитьЧисло("7");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка8ПраваяКлавиатура(Команда)

	ДобавитьЧисло("8");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка9ПраваяКлавиатура(Команда)

	ДобавитьЧисло("9");

КонецПроцедуры

&НаКлиенте
Процедура КнопкаТочка(Команда)

	Если РежимВвода = 1 Тогда
		Если ПервоеНажатие Тогда
			ПервоеНажатие = Ложь;
			СуммаСтрокой = "0.";
			ПозицияТочки = 2;
		КонецЕсли;
		Если ПозицияТочки = 0 Тогда
			СуммаСтрокой = СуммаСтрокой + ".";
			ПозицияТочки = СтрДлина(СуммаСтрокой);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Стереть(Команда)

	Если РежимВвода = 1 Тогда
		ПоследнийСимвол = Прав(СуммаСтрокой, 1);
		Если ПоследнийСимвол = "." Тогда
			ПозицияТочки = 0;
			ПервоеНажатие = Истина;
		КонецЕсли;
		СуммаСтрокой = Лев(СуммаСтрокой, СтрДлина(СуммаСтрокой) - 1);
	Иначе
		НомерСчета = Лев(НомерСчета, СтрДлина(НомерСчета) - 1);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Выход(Команда)

	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСведения(Команда)

	СведенияОСчете = ПолучитьСведенияОСчете();

	ПолученыСведенияПоСчету = СведенияОСчете.Результат;
	ОстатокБаллов = СведенияОСчете.Сумма;

	Если ПолученыСведенияПоСчету Тогда
		Если РежимВвода = 0 Тогда
			РежимВвода = 1;
		Иначе
			РезультатВводаСчетUmcio = UMC_ОбменСUmicoВызовСервера.НоваяСтруктураРезультатВводаСчетUmcio();
			РезультатВводаСчетUmcio.Сумма = 0;
			РезультатВводаСчетUmcio.НомерСчета = НомерСчета;
			Закрыть(РезультатВводаСчетUmcio);
		КонецЕсли;
	КонецЕсли;

	УстановитьВидимость();

КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)

	Сумма = РозничныеПродажиКлиентСервер.ПривестиСтрокуКЧислу(СуммаСтрокой);

	Если Сумма > ОстатокБаллов Тогда
		ТекстСообщения = НСтр("ru = 'Введенное значение превышает остаток бонусов по счету.'");
		ПоказатьПредупреждение( , ТекстСообщения);
		Возврат;
	КонецЕсли;

	РезультатВводаСчетUmcio = UMC_ОбменСUmicoВызовСервера.НоваяСтруктураРезультатВводаСчетUmcio();
	РезультатВводаСчетUmcio.Сумма = Сумма;
	РезультатВводаСчетUmcio.НомерСчета = НомерСчета;

	Закрыть(РезультатВводаСчетUmcio);

КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)

	РежимВвода = 0;

	УстановитьВидимость();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьЧисло(ЧислоСтрокой)

	Если РежимВвода = 1 Тогда

		Точность = 2;

		Если Не (ПозицияТочки > 0 И СтрДлина(СуммаСтрокой) >= ПозицияТочки + Точность) Тогда
			Если ПервоеНажатие Тогда
				ПервоеНажатие=Ложь;
				СуммаСтрокой = "";
			КонецЕсли;
			Если СуммаСтрокой = "0" Тогда
				СуммаСтрокой = "";
			КонецЕсли;
			СуммаСтрокой = СуммаСтрокой + ЧислоСтрокой;
		КонецЕсли;

	Иначе

		НомерСчета = НомерСчета + ЧислоСтрокой;

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОпределитьПараметрыПодключения()

	Значение = РеквизитФормыВЗначение("Объект");
	Значение.ОпределитьПараметрыПодключения();
	ЗначениеВРеквизитФормы(Значение, "Объект");

КонецПроцедуры

&НаСервере
Функция ПолучитьСведенияОСчете()

	Значение = РеквизитФормыВЗначение("Объект");

	НомерСчетаБезПробелов = СтрЗаменить(НомерСчета, " ", "");

	Возврат Значение.ПолучитьСведенияОСчете(НомерСчетаБезПробелов);

КонецФункции

&НаСервере
Процедура УстановитьВидимость()

	Если ПолученыСведенияПоСчету = Неопределено Тогда
		Если РежимВвода = 2 Тогда
			Элементы.ИнформационнаяНадпись.Заголовок = НСтр(
			"ru = 'Введите номер счета Umico, на который будут начислены бонусные баллы.'");
		Иначе
			Элементы.ИнформационнаяНадпись.Заголовок = НСтр(
			"ru = 'Введите номер счета Umico, чтобы получить сведения о балансе.'");
		КонецЕсли;
		Элементы.ИнформационнаяНадпись.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
	ИначеЕсли ПолученыСведенияПоСчету = Истина Тогда
		Элементы.ИнформационнаяНадпись.Заголовок = СтрШаблон(НСтр("ru = 'Остаток баллов на счете: %1'"), Формат(
			ОстатокБаллов, "ЧДЦ=2; ЧН=0;"));
		Элементы.ИнформационнаяНадпись.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
	ИначеЕсли ПолученыСведенияПоСчету = Ложь Тогда
		Элементы.ИнформационнаяНадпись.Заголовок = НСтр("ru = 'Не удалось получить сведения о счете.'");
		Элементы.ИнформационнаяНадпись.ЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
	КонецЕсли;

	Если РежимВвода = 1 Тогда
		Элементы.ОК.КнопкаПоУмолчанию = Истина;
		Элементы.СтраницыШапки.ТекущаяСтраница = Элементы.СтраницаВводСуммы;
	Иначе
		Элементы.ПолучитьСведения.КнопкаПоУмолчанию = Истина;
		Элементы.СтраницыШапки.ТекущаяСтраница = Элементы.СтраницаНомерСчета;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти