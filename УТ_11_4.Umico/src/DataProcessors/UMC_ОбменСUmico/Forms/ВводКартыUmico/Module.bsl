// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не Параметры.Свойство("РежимВвода", РежимВвода) Тогда
		РежимВвода = 0;
	КонецЕсли;

	Параметры.Свойство("НомерКарты", НомерКарты);
	Параметры.Свойство("ВариантИспользования", Объект.ВариантИспользования);
	Параметры.Свойство("КассаККМ", Объект.КассаККМ);

	ОпределитьПараметрыПодключения();

	ПервоеНажатие = Истина;

	УстановитьВидимость();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если Не ЗначениеЗаполнено(Объект.APIKey) Или Не ЗначениеЗаполнено(Объект.POSID) Тогда

		ТекстСообщения = НСтр(
			"ru = 'Не определены параметры подключения к Umico. Пожалуйста, обратитесь к администратору.'");
		ПоказатьПредупреждение( , ТекстСообщения);

		Закрыть();

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Кнопка0(Команда)

	ДобавитьЧисло("0");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка1(Команда)

	ДобавитьЧисло("1");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка2(Команда)

	ДобавитьЧисло("2");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка3(Команда)

	ДобавитьЧисло("3");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка4(Команда)

	ДобавитьЧисло("4");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка5(Команда)

	ДобавитьЧисло("5");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка6(Команда)

	ДобавитьЧисло("6");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка7(Команда)

	ДобавитьЧисло("7");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка8(Команда)

	ДобавитьЧисло("8");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка9(Команда)

	ДобавитьЧисло("9");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка0ПраваяКлавиатура(Команда)

	ДобавитьЧисло("0");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка1ПраваяКлавиатура(Команда)

	ДобавитьЧисло("1");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка2ПраваяКлавиатура(Команда)

	ДобавитьЧисло("2");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка3ПраваяКлавиатура(Команда)

	ДобавитьЧисло("3");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка4ПраваяКлавиатура(Команда)

	ДобавитьЧисло("4");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка5ПраваяКлавиатура(Команда)

	ДобавитьЧисло("5");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка6ПраваяКлавиатура(Команда)

	ДобавитьЧисло("6");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка7ПраваяКлавиатура(Команда)

	ДобавитьЧисло("7");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка8ПраваяКлавиатура(Команда)

	ДобавитьЧисло("8");

КонецПроцедуры

&НаКлиенте
Процедура Кнопка9ПраваяКлавиатура(Команда)

	ДобавитьЧисло("9");

КонецПроцедуры

&НаКлиенте
Процедура КнопкаТочка(Команда)

	Если РежимВвода = 1 Тогда
		Если ПервоеНажатие Тогда
			ПервоеНажатие = Ложь;
			СуммаСтрокой = "0.";
			ПозицияТочки = 2;
		КонецЕсли;
		Если ПозицияТочки = 0 Тогда
			СуммаСтрокой = СуммаСтрокой + ".";
			ПозицияТочки = СтрДлина(СуммаСтрокой);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Стереть(Команда)

	Если РежимВвода = 1 Тогда
		ПоследнийСимвол = Прав(СуммаСтрокой, 1);
		Если ПоследнийСимвол = "." Тогда
			ПозицияТочки = 0;
			ПервоеНажатие = Истина;
		КонецЕсли;
		СуммаСтрокой = Лев(СуммаСтрокой, СтрДлина(СуммаСтрокой) - 1);
	Иначе
		НомерКарты = Лев(НомерКарты, СтрДлина(НомерКарты) - 1);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Выход(Команда)

	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСведения(Команда)

	СведенияОКарте = ПолучитьСведенияОКарте();

	ПолученыСведенияПоКарте = СведенияОКарте.Результат;
	ОстатокБаллов = СведенияОКарте.Сумма;

	Если ПолученыСведенияПоКарте Тогда
		Если РежимВвода = 0 Тогда
			РежимВвода = 1;
		Иначе
			РезультатВводаКартыUmico = UMC_ОбменСUmicoВызовСервера.НоваяСтруктураРезультатВводаКартыUmico();
			РезультатВводаКартыUmico.Сумма = 0;
			РезультатВводаКартыUmico.НомерКарты = НомерКарты;
			Закрыть(РезультатВводаКартыUmico);
		КонецЕсли;
	КонецЕсли;

	УстановитьВидимость();

КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)

	Сумма = РозничныеПродажиКлиентСервер.ПривестиСтрокуКЧислу(СуммаСтрокой);

	Если Сумма > ОстатокБаллов Тогда
		ТекстСообщения = НСтр("ru = 'Введенное значение превышает остаток бонусов на карте.'");
		ПоказатьПредупреждение( , ТекстСообщения);
		Возврат;
	КонецЕсли;

	РезультатВводаКартыUmico = UMC_ОбменСUmicoВызовСервера.НоваяСтруктураРезультатВводаКартыUmico();
	РезультатВводаКартыUmico.Сумма = Сумма;
	РезультатВводаКартыUmico.НомерКарты = НомерКарты;

	Закрыть(РезультатВводаКартыUmico);

КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)

	РежимВвода = 0;

	УстановитьВидимость();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьЧисло(ЧислоСтрокой)

	Если РежимВвода = 1 Тогда

		Точность = 2;

		Если Не (ПозицияТочки > 0 И СтрДлина(СуммаСтрокой) >= ПозицияТочки + Точность) Тогда
			Если ПервоеНажатие Тогда
				ПервоеНажатие=Ложь;
				СуммаСтрокой = "";
			КонецЕсли;
			Если СуммаСтрокой = "0" Тогда
				СуммаСтрокой = "";
			КонецЕсли;
			СуммаСтрокой = СуммаСтрокой + ЧислоСтрокой;
		КонецЕсли;

	Иначе

		НомерКарты = НомерКарты + ЧислоСтрокой;

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОпределитьПараметрыПодключения()

	Значение = РеквизитФормыВЗначение("Объект");
	Значение.ОпределитьПараметрыПодключения();
	ЗначениеВРеквизитФормы(Значение, "Объект");

КонецПроцедуры

&НаСервере
Функция ПолучитьСведенияОКарте()

	Значение = РеквизитФормыВЗначение("Объект");

	НомерКартыБезПробелов = СтрЗаменить(НомерКарты, " ", "");

	Возврат Значение.ПолучитьСведенияОКарте(НомерКартыБезПробелов);

КонецФункции

&НаСервере
Процедура УстановитьВидимость()

	Если ПолученыСведенияПоКарте = Неопределено Тогда
		Если РежимВвода = 2 Тогда
			Элементы.ИнформационнаяНадпись.Заголовок = НСтр(
			"ru = 'Введите номер карты Umico, на которую будут начислены бонусные баллы.'");
		Иначе
			Элементы.ИнформационнаяНадпись.Заголовок = НСтр(
			"ru = 'Введите номер карты Umico, чтобы получить сведения о балансе.'");
		КонецЕсли;
		Элементы.ИнформационнаяНадпись.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
	ИначеЕсли ПолученыСведенияПоКарте = Истина Тогда
		Элементы.ИнформационнаяНадпись.Заголовок = СтрШаблон(НСтр("ru = 'Остаток баллов на карте: %1'"), Формат(
			ОстатокБаллов, "ЧДЦ=2; ЧН=0;"));
		Элементы.ИнформационнаяНадпись.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
	ИначеЕсли ПолученыСведенияПоКарте = Ложь Тогда
		Элементы.ИнформационнаяНадпись.Заголовок = НСтр("ru = 'Не удалось получить сведения о карте.'");
		Элементы.ИнформационнаяНадпись.ЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
	КонецЕсли;

	Если РежимВвода = 1 Тогда
		Элементы.ОК.КнопкаПоУмолчанию = Истина;
		Элементы.СтраницыШапки.ТекущаяСтраница = Элементы.СтраницаВводСуммы;
	Иначе
		Элементы.ПолучитьСведения.КнопкаПоУмолчанию = Истина;
		Элементы.СтраницыШапки.ТекущаяСтраница = Элементы.СтраницаНомерКарты;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти