// @strict-types

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура UMC_UMC_ОплатитьБалламиUmicoПосле(Команда)

	UMC_ДополнительныеПараметры = Новый Структура;
	//@skip-check wrong-string-literal-content
	//@skip-check notify-description-to-server-procedure
	UMC_ДополнительныеПараметры.Вставить("ОповещениеВФормуОплаты", Новый ОписаниеОповещения("ОплатаЗавершение",
		ЭтотОбъект));
	UMC_ДополнительныеПараметры.Вставить("ПолученоНаличными", РозничныеПродажиКлиентСервер.ПривестиСтрокуКЧислу(
		ПолученоНаличными));

	//@skip-check wrong-string-literal-content
	ВыполнитьОбработкуОповещения(
		Новый ОписаниеОповещения("UMC_ДобавитьОплатуБалламиUmico", ВладелецФормы, UMC_ДополнительныеПараметры), Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&Вместо("РассчитатьИтоги")
&НаКлиентеНаСервереБезКонтекста
// ИзменениеИКонтроль не используется по причине нестабильности.
//@skip-check property-return-type
Процедура UMC_РассчитатьИтоги(Форма, ИнформацияОбОплате)

	Форма.СуммаОплаты = РозничныеПродажиКлиентСервер.ПривестиСтрокуКЧислу(Форма.ПолученоНаличными)
	                  + Форма.ОплаченоБонуснымиБаллами
	                  + Форма.ОплаченоПлатежнымиКартами
	                  + Форма.ОплаченоСертификатами;

	//#Вставка
		Форма.СуммаОплаты = Форма.СуммаОплаты + Форма.UMC_ОплаченоБалламиUmico;
	//#КонецВставки

	Если Форма.СуммаОплаты >= Форма.КОплате Тогда

		Форма.СуммаСдачи   = Форма.СуммаОплаты - Форма.КОплате;
		Форма.СуммаДоплаты = 0;

		Форма.Элементы.ГруппаИтого.ТекущаяСтраница = Форма.Элементы.ГруппаСдача;
		Форма.Элементы.КомандаПробитьЧек.Доступность = Истина;

		Форма.ТекущийЭлемент = Форма.Элементы.КомандаПробитьЧек;

	Иначе

		Форма.СуммаСдачи   = 0;
		Форма.СуммаДоплаты = Форма.КОплате - Форма.СуммаОплаты;

		Форма.Элементы.ГруппаИтого.ТекущаяСтраница = Форма.Элементы.ГруппаДоплата;
		Форма.Элементы.КомандаПробитьЧек.Доступность = Ложь;

	КонецЕсли;

	Если Форма.ЭтоВозврат И ТипЗнч(ИнформацияОбОплате) = Тип("Структура") Тогда
		Форма.Элементы.ОплатаПлатежнаяКарта.Доступность = (ИнформацияОбОплате.ПлатежныеКарты - ИнформацияОбОплате.ПлатежныеКартыОтменено > 0);
	КонецЕсли;

КонецПроцедуры

// UMC оплата завершение.
// 
// Параметры:
//  ИнформацияОбОплате - Структура:
// * БаллыUmico - Число
//  ДополнительныеПараметры - Структура
&Перед("ОплатаЗавершение")
&НаКлиенте
Процедура UMC_ОплатаЗавершение(ИнформацияОбОплате, ДополнительныеПараметры) Экспорт

	UMC_ОплаченоБалламиUmico = ИнформацияОбОплате.БаллыUmico;

КонецПроцедуры

#КонецОбласти