
#Область СлужебныеПроцедурыИФункции

&Вместо("ИнформацияОбОплате")
&НаКлиентеНаСервереБезКонтекста
// ИзменениеИКонтроль не используется по причине нестабильности.
//@skip-check extension-variable-prefix
Функция UMC_ИнформацияОбОплате(Форма)

	ОплатаПлатежнымиКартамиОтменено = 0;
	Для Каждого СтрокаТЧ Из Форма.Объект.ОплатаПлатежнымиКартами Цикл
		Если СтрокаТЧ.ОплатаОтменена Тогда
			ОплатаПлатежнымиКартамиОтменено = ОплатаПлатежнымиКартамиОтменено + СтрокаТЧ.Сумма;
		КонецЕсли;
	КонецЦикла;

	Форма.СуммаДокумента = ЦенообразованиеКлиентСервер.ПолучитьСуммуДокумента(Форма.Объект.Товары, Форма.Объект.ЦенаВключаетНДС);

	Если Форма.Объект.ЦенаВключаетНДС Тогда
		СуммаБезСкидки = Форма.СуммаДокумента;
	Иначе
		СуммаБезСкидки = Форма.СуммаДокумента - Форма.Объект.Товары.Итог("СуммаНДС");
	КонецЕсли;

	ИнформацияОбОплате = Новый Структура;
	ИнформацияОбОплате.Вставить("Документ",               Форма.Объект.Ссылка);

	ИнформацияОбОплате.Вставить("Наличные",               Форма.Объект.ВыданоНаличными);
	ИнформацияОбОплате.Вставить("ПлатежныеКарты",         Форма.Объект.ОплатаПлатежнымиКартами.Итог("Сумма"));
	ИнформацияОбОплате.Вставить("ПлатежныеКартыОтменено", ОплатаПлатежнымиКартамиОтменено);
	ИнформацияОбОплате.Вставить("ПодарочныеСертификаты",  0);
	ИнформацияОбОплате.Вставить("БонусныеБаллы",          0);

	ИнформацияОбОплате.Вставить("СуммаДокумента",        Форма.СуммаДокумента);
	ИнформацияОбОплате.Вставить("СуммаКОплате",          СуммаБезСкидки);
	ИнформацияОбОплате.Вставить("СуммаСкидки",           0);
	ИнформацияОбОплате.Вставить("ИтогоОплачено",         ИнформацияОбОплате.Наличные + ОплатаПлатежнымиКартамиОтменено);

	ИнформацияОбОплате.Вставить("ДоступныеВидыОплаты", ДоступныеВидыОплаты(Форма));

	//#Вставка
	ИнформацияОбОплате
	.Вставить("БаллыUmico", Форма.Объект.UMC_ОплаченноБалламиUmico);
	ИнформацияОбОплате.Вставить("ИтогоОплачено", ИнформацияОбОплате.Наличные + ОплатаПлатежнымиКартамиОтменено
		+ ИнформацияОбОплате.БаллыUmico);
	ИнформацияОбОплате.Вставить("СуммаКОплате", СуммаБезСкидки - ИнформацияОбОплате.БаллыUmico);
	
	Форма.ИнформационнаяПанельСуммаКВозвратуUmico = ИнформацияОбОплате.БаллыUmico;
	//#КонецВставки

	Возврат ИнформацияОбОплате;

КонецФункции

// UMC пробить чек завершение.
// 
// Параметры:
//  Результат - Структура - Результат:
//  * ВыполненаОперацияНаУстройстве - Булево
//  * ИзмененныеДанныеЗаписаны - Булево
//  ДополнительныеПараметры  - Структура, Неопределено - Дополнительные параметры
&После("ПробитьЧекЗавершение")
&НаКлиенте
Процедура UMC_ПробитьЧекЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат.ВыполненаОперацияНаУстройстве И Результат.ИзмененныеДанныеЗаписаны И ЗначениеЗаполнено(
		Объект.UMC_НомерКартыUmico) Тогда

		UMC_ЗапуститьПередачуИнформацииОЧекеВUmico();	

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура UMC_ЗапуститьПередачуИнформацииОЧекеВUmico()
	
	UMC_СтруктураОтвета = UMC_ПередатьИнформациюОЧекеВUmico();
	Если Не UMC_СтруктураОтвета.Результат Тогда
		
		Если ЗначениеЗаполнено(UMC_СтруктураОтвета.ТекстОшибки) Тогда
			UMC_ТекстСообщения = СтрШаблон(НСтр(
				"ru = 'Не удалось передать сведения о документе в Umico по причине - %1. Повторить попытку?'"),
				UMC_СтруктураОтвета.ТекстОшибки);
		Иначе
			UMC_ТекстСообщения = НСтр("ru = 'Не удалось передать сведения о документе в Umico. Повторить попытку?'");
		КонецЕсли;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("UMC_ПробитьЧекОшибкаЗавершение", ЭтотОбъект), UMC_ТекстСообщения,
			РежимДиалогаВопрос.ДаНет, 10);
			
	КонецЕсли;
	
КонецПроцедуры

// Передает информацию о чеке в Umico.
// 
// Возвращаемое значение:
// см. UMC_ОбменСUmico.НоваяСтруктураРезультатРегистрацииЧекаВUmico
&НаСервере
Функция UMC_ПередатьИнформациюОЧекеВUmico()

	UMC_ОбработкаОбменаСUmico = Обработки.UMC_ОбменСUmico.Создать();
	UMC_ОбработкаОбменаСUmico.КассаККМ = Объект.КассаККМ;
	UMC_ОбработкаОбменаСUmico.ВариантИспользования = Перечисления.UMC_ВариантыИспользованияПодключенияКUmico.ДляРозничныхПродаж;

	Если UMC_ОбработкаОбменаСUmico.ОпределитьПараметрыПодключения() Тогда

		Возврат UMC_ОбработкаОбменаСUmico.ЗарегистрироватьЧек(Объект.Ссылка);
		
	Иначе

		UMC_Ответ = UMC_ОбменСUmico.НоваяСтруктураРезультатРегистрацииЧекаВUmico();
		UMC_Ответ.ТекстОшибки = UMC_ОбменСUmico.ПолучитьТекстОшибкиНеОпределеныПараметрыПодключения();
		Возврат UMC_Ответ;
		
	КонецЕсли;

КонецФункции

// UMC пробить чек ошибка завершение.
// 
// Параметры:
//  Результат - КодВозвратаДиалога - Результат
//  ДополнительныеПараметры - Структура, Неопределено - Дополнительные параметры
&НаКлиенте
Процедура UMC_ПробитьЧекОшибкаЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		UMC_ЗапуститьПередачуИнформацииОЧекеВUmico();
	КонецЕсли;

КонецПроцедуры

&Перед("СмешаннаяОплатаОбработкаОповещения")
&НаКлиенте
Процедура UMC_СмешаннаяОплатаОбработкаОповещения(Результат, ДополнительныеПараметры) Экспорт
	
	UMC_ВыполнитьПредварительныйРасчетВозвратаНаUmico();
	
КонецПроцедуры

&Перед("ОплатитьНаличнымиОбработкаОповещения")
&НаКлиенте
Процедура UMC_ОплатитьНаличнымиОбработкаОповещения(Результат, ДополнительныеПараметры) Экспорт
	
	UMC_ВыполнитьПредварительныйРасчетВозвратаНаUmico();
	
КонецПроцедуры

&НаКлиенте
Процедура UMC_ВыполнитьПредварительныйРасчетВозвратаНаUmico()
	
	Если ЗначениеЗаполнено(Объект.UMC_НомерКартыUmico) Тогда
		ЗаписатьНаКлиенте();
		UMC_ДанныеПредварительнойРегистрации = UMC_ПолучитьСуммуОплатыБалламиUmico();
		Если UMC_ДанныеПредварительнойРегистрации.Результат Тогда
			Объект.UMC_ОплаченноБалламиUmico = UMC_ДанныеПредварительнойРегистрации.СуммаБалловКВозврату;
		Иначе
			UMC_ТекстПредупреждения = СтрШаблон(НСтр(
				"ru = 'ВНИМАНИЕ! Не удалось получить предварительные данные для расчета возвращаемой суммы на карту Umico! %1'"),
				UMC_ДанныеПредварительнойРегистрации.ТекстОшибки);
			ПоказатьПредупреждение( , UMC_ТекстПредупреждения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// UMC получить сумму оплаты баллами Umico.
// 
// Возвращаемое значение:
//  см. UMC_ОбменСUmico.НоваяСтруктураРезультатРегистрацииЧекаВUmico
&НаСервере
Функция UMC_ПолучитьСуммуОплатыБалламиUmico()
	
	UMC_ОбработкаОбменаСUmico = Обработки.UMC_ОбменСUmico.Создать();
	UMC_ОбработкаОбменаСUmico.КассаККМ = Объект.КассаККМ;
	UMC_ОбработкаОбменаСUmico.ВариантИспользования = Перечисления.UMC_ВариантыИспользованияПодключенияКUmico.ДляРозничныхПродаж;

	Если UMC_ОбработкаОбменаСUmico.ОпределитьПараметрыПодключения() Тогда

		Возврат UMC_ОбработкаОбменаСUmico.ПредварительнаяРегистрацияЧекаПередВозвратом(Объект.Ссылка);
		
	Иначе

		UMC_Ответ = UMC_ОбменСUmico.НоваяСтруктураРезультатРегистрацииЧекаВUmico();
		UMC_Ответ.ТекстОшибки = UMC_ОбменСUmico.ПолучитьТекстОшибкиНеОпределеныПараметрыПодключения();
		Возврат UMC_Ответ;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти